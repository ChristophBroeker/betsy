apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "application"

// Main class of betsy
mainClassName = "betsy.Main"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy', 'src/main/tests', 'src/main/java']
        }
    }
}

ext {
    config = new ConfigSlurper().parse(new File("Config.groovy").toURI().toURL())
}

configurations {
    internal
    external
}

dependencies {
    internal 'org.codehaus.groovy:groovy-all:2.2.0'

    internal 'org.apache.ant:ant:1.9.2'
    internal 'log4j:log4j:1.2.17'
    internal 'org.virtualbox:vboxjws:4.2.8'
    internal 'commons-daemon:commons-daemon:1.0.12'
    internal 'org.apache.commons:commons-io:1.3.2'
    internal 'commons-lang:commons-lang:2.4'
    internal 'org.apache.ant:ant-apache-log4j:1.9.2'

    testCompile 'junit:junit:4.11'

    external files(fileTree(dir: "${config.soapui.home}/bin", includes: ['*.jar']))
    external files(fileTree(dir: "${config.soapui.home}/lib", includes: ['*.jar'], excludes: ['groovy*', 'log4j*', 'commons-io*', 'commons-lang*']))

    compile configurations.internal
    compile configurations.external
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

task downloadSoapUI {
    outputs.file new File("${config.downloads.dir}/${config.soapui.download.filename}")

    doLast {
        ant.mkdir dir: config.downloads.dir
        ant.get dest: config.downloads.dir, {
            ant.url url: config.soapui.download.url
        }
    }
}

task installSoapUI {
    ext.srcFile = new File("${config.downloads.dir}/${config.soapui.download.filename}")
    ext.destDir = new File(config.soapui.container)

    inputs.file srcFile
    outputs.dir destDir

    doLast {
        ant.mkdir dir: new File(config.soapui.home)
        ant.unzip src: srcFile, dest: destDir
    }
}

task downloadAnt {
    outputs.file new File("${config.downloads.dir}/${config.ant.download.filename}")

    doLast {
        ant.mkdir dir: config.downloads.dir
        ant.get dest: config.downloads.dir, {
            ant.url url: config.ant.download.url
        }
    }
}

task installAnt {
    ext.srcFile = new File("${config.downloads.dir}/${config.ant.download.filename}")
    ext.destDir = new File(config.ant.container)

    inputs.file srcFile
    outputs.dir new File(config.ant.home)

    doLast {
        ant.mkdir dir: destDir
        ant.unzip src: srcFile, dest: destDir
    }

}

installSoapUI.dependsOn downloadSoapUI
compileGroovy.dependsOn installSoapUI
installAnt.dependsOn downloadAnt
installSoapUI.dependsOn installAnt

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

task enginecontrol(type: JavaExec, dependsOn: build) {
    description = 'Starts Engine Control GUI'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.EngineControl"
}

task partnerservice(type: JavaExec, dependsOn: build) {
    description = 'Starts test case partner service only'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.PartnerServiceControl"
}

/**
 * Creates jar file for the virtual machines.
 */
task jarServer(type: Jar, dependsOn: classes) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.resources)

    // results in betsy-vms.jar
    classifier = 'vms'
    baseName = 'betsy'

    manifest {
        attributes 'Main-Class': 'betsy.virtual.server.BetsyVirtualMachineServer', 'Implementation-Title': 'Betsy-VirtualMachineServer', 'Built-Date': new Date()
    }

    // remove MANIFEST.MF files previously inserted
    doFirst {
        from(configurations.internal.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude 'META-INF/MANIFEST.MF'
        }
    }
}
