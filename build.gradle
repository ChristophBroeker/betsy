apply plugin: "groovy"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "application"

mainClassName = "configuration.TestUsingParameters"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main/groovy'
            srcDir 'src/main/tests'

        }
    }
}

ext {
    downloadsDir = "downloads"
    soapuiArchiveName = "SoapUI-4.6.1-windows-bin.zip"
    soapuiDownloadUrl = "https://lspi.wiai.uni-bamberg.de/svn/betsy/${soapuiArchiveName}"
    soapuiDownloadedFile = "${downloadsDir}/${soapuiArchiveName}"
    soapuiDir = "soapui"
    soapuiInstallDir = "${soapuiDir}/SoapUI-4.6.1"
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.1.2'
    compile 'org.apache.ant:ant:1.8.4'
    compile files(fileTree(dir: "${soapuiInstallDir}\\lib", includes: ['*.jar']))
    compile files(fileTree(dir: "${soapuiInstallDir}\\bin", includes: ['*.jar']))
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

task installSoapUI << {
    ant.get dest: downloadsDir, skipexisting: true, {
        ant.url url: soapuiDownloadUrl
    }

    if(!new File(soapuiInstallDir).exists()) {
        ant.mkdir dir: soapuiDir
        ant.unzip src: soapuiDownloadedFile, dest: soapuiDir
    }
}
compileJava.dependsOn installSoapUI

task checkRunRequirements << {
    // check ant existence
    try {
        "ant.bat -v".execute()
    } catch (IOException e){
        throw new IllegalStateException("ant is not available on the PATH", e)
    }
}
run.dependsOn checkRunRequirements

run {
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

task enginecontrol (type: JavaExec, dependsOn: build){
    description = 'Starts Engine Control GUI'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.EngineControl"
}

task partnerservice (type: JavaExec, dependsOn: build){
    description = 'Starts test case partner service only'
    classpath = sourceSets.main.runtimeClasspath
    main = "betsy.tool.PartnerServiceControl"
}
